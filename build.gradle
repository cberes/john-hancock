plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

group = 'net.seabears'
version = '1.0.2-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'commons-io:commons-io:2.5'
}

test {
    systemProperty 'test.image.path', "$buildDir/test-images"
}

jacocoTestReport {
    reports {
        csv.enabled = false
        html.enabled = true
        xml.enabled = true // coveralls plugin depends on xml format report
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'john-hancock'
                packaging 'jar'
                description 'Build images from common customer-signature data serialization formats.'
                url 'https://github.com/cberes/john-hancock'

                scm {
                    url 'https://github.com/cberes/john-hancock.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://mit-license.org/license.txt'
                    }
                }

                developers {
                    developer {
                        id 'cberes'
                        name 'Corey Beres'
                        email 'corey.beres@gmail.com'
                    }
                }
            }
        }
    }
}
